plugins {
    id 'java'
    id 'idea'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id 'org.jetbrains.kotlin.jvm'
    id 'org.jetbrains.kotlin.plugin.serialization'
    id 'org.jetbrains.kotlin.kapt'
}

apply from: 'https://raw.githubusercontent.com/MineInAbyss/shared-gradle/master/common.gradle'

group 'com.mineinabyss'
version plugin_version

repositories {
    mavenCentral()
    jcenter()
//    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' } //Spigot
    maven { url 'https://papermc.io/repo/repository/maven-public/' } //Paper
    maven { url 'https://repo.mineinabyss.com/releases'}
    maven { url 'https://jitpack.io' }

    mavenLocal()
}

dependencies {
    compileOnly "com.destroystokyo.paper:paper-api:$server_version"
//    compileOnly "org.spigotmc:spigot:$server_version" // NMS
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    compileOnly platform("com.mineinabyss:kotlinspice:$kotlin_version+")
    compileOnly "com.github.okkero:skedule"

    compileOnly "com.mineinabyss:geary-spigot:0.3.27"
    implementation "com.mineinabyss:idofront:0.5.8"
}

shadowJar {
    archivesBaseName = "Looty"

    relocate 'com.derongan.minecraft.guiy', "${project.group}.${project.name}.guiy".toLowerCase()
    relocate 'com.mineinabyss.idofront', "${project.group}.${project.name}.idofront".toLowerCase()

    minimize {
        exclude(dependency('de.erethon:headlib:3.0.2'))
        exclude(dependency('com.github.WesJD.AnvilGUI:anvilgui:5e3ab1f721'))
    }
}
build.dependsOn shadowJar

publishing {
    repositories {
        maven {
            url = "https://repo.mineinabyss.com/releases"
            credentials {
                username = findProperty('mineinabyssMavenUsername')
                password = findProperty('mineinabyssMavenPassword')
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
        }
    }
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlinx.serialization.ExperimentalSerializationApi"
    }
}
compileTestKotlin { kotlinOptions { jvmTarget = "1.8" } }
